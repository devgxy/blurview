plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id 'signing'
    id("org.jetbrains.dokka") version "1.9.20"
}

tasks.register('androidSourcesJar', Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.source
    from android.sourceSets.main.res.srcDirs
}
tasks.register('androidJavaDoc', Jar) {
    archiveClassifier.set('javadoc')
    from tasks.named('dokkaJavadoc').get().outputDirectory
    dependsOn tasks.named('dokkaJavadoc')
}
version = "1.0.0-SNAPSHOT"

android {
    namespace 'com.google.android.renderscript'
    compileSdk 34

    defaultConfig {
        minSdk 21
        consumerProguardFiles "consumer-rules.pro"

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++17"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }

    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = 'io.github.devgxy'
                artifactId = 'renderscript-toolkit'
                version = '1.0.0-SNAPSHOT'

                artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
                artifact(androidSourcesJar)
                artifact(androidJavaDoc)

                pom {
                    name = 'renderscript-toolkit'
                    description = 'A Gaussian Blur implementation for android view'
                    url = 'https://github.com/devgxy/blurview/'

                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'gxy'
                            name = 'gxy'
                            email = 'guoxiangyanggxy@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/devgxy/blurview.git'
                        developerConnection = 'scm:git:ssh://github.com:devgxy/blurview.git'
                        url = 'https://github.com/devgxy/blurview'
                    }
                }
            }
        }

        repositories {
            maven {
                name = "mavenCentral"  // 修改为 sonatype，确保一致性
                def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }
        }
    }

    signing {
        useGpgCmd()
        sign publishing.publications.release
    }
}




dependencies {
    implementation 'androidx.core:core-ktx:1.13.0'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.9.0'
}